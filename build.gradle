plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.3.2'
}

apply plugin: 'groovy'
apply plugin: 'java'

group 'com.rundeck.plugin'

ext.rundeckPluginVersion = '1.2'
ext.pluginClassNames='com.rundeck.plugin.OptionFormatResourceModel'
ext.pluginName = 'Option JSON Resource Format Plugin'
ext.pluginDescription = 'Option JSON Resource Format Plugin'


scmVersion {
    tag {
        prefix = ''
        versionSeparator = ''
        def origDeserialize=deserialize
        //apend .0 to satisfy semver if the tag version is only X.Y
        deserialize = { config, position, tagName ->
            def orig = origDeserialize(config, position, tagName)
            if (orig.split('\\.').length < 3) {
                orig += ".0"
            }
            orig
        }
    }
}
project.version = scmVersion.version


sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    pluginLibs

    compile {
        extendsFrom pluginLibs
    }
}


dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    compile group: 'org.rundeck', name: 'rundeck-core', version: '3.0.5-20180828'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}


task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/' + it.name}.join(' ')
        attributes 'Rundeck-Plugin-Name' : pluginName
        attributes 'Rundeck-Plugin-Description' : 'This plugin serializes a set of Node resources into a JSON format that is compatible with Remote option values on a Job For example, this plugin can help to control the node filter of a Job through input options (for example using Cascade remote options).'
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '3.0.5+'
        attributes 'Rundeck-Plugin-Tags': 'java,resource format generator,json'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/resource-format-json-option'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Author': 'Rundeck, Inc.'
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': project.version
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
        attributes 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
    dependsOn(copyToLib)
}